Chapter 5 (2/2) - Macros
===================

## Hygiene

- 매크로는 다른 코드에 embedded 되는 경우가 많은데, 이 때 매크로에서의 바인딩이 기존 코드의 바인딩과 잘못 엮여 버그를 일으키는 경우가 있으며, 이러한 버그는 원인을 파악하기 어려움.

```clojure
(defmacro unhygienic 
    [& body]
    `(let [x :oops] 
        ~@body))
;=> #'user/unhygenic

(unhygienic (println "x:" x))
;=> CompilerException java.lang.RuntimeException: Can't let qualified name: user/x
```
- `let`은 식별되지 않은(unqualified) 심벌에 대한 바인딩을 수행하는데 여기서는 `user/x`에 대해 바인딩을 시도하므로 컴파일 에러(macro expansion 시)가 발생됨 
→ 이를 피하기 위해 quoting과 unquoting을 활용하여 다시 작성해보면,

```clojure
(defmacro still-unhygienic 
    [& body]
      `(let [~'x :oops] ~@body))   ;; namespace 식별을 피할 수 있도록 변경
;=> #'user/still-unhygenic

(macroexpand-1 `(still-unhygienic
                  (println "x:" x))) 
;=> (clojure.core/let [x :oops]     ;; 확장결과에 user/x 가 아닌 x 가 포함됨
;=>     (clojure.core/println "x:" user/x))

(still-unhygienic (println "x:" x)) 
;=> x: :oops
```

그러나, 매크로의 로컬 범위 안과 밖에서 동일한 이름 `x`를 바인딩하고 있어 문제가 있다.

```clojure
(let [x :this-is-important] 
    (still-unhygienic
        (println "x:" x))) 
; x: :oops
;; :this-is-important 가 출력되지 않음. 매크로에서 let으로 정의된 x가 로컬 x를 덮어써버린다
```

클로저에서는 이를 위한 해결책을 제공한다.

### Gensyms to the Rescue

- `gensym` 함수: unique한 심벌을, 호출 때마다 새로 생성하여 리턴 → hygienic 매크로 작성에 도움을 줌

```clojure
(gensym) 
;=> G__2386

(gensym)
;=> G__2391

(gensym "sym") 
;=> sym2396 

(gensym "sym") 
;=> sym2402
```

- `gensym`을 활용한 매크로 작성

```clojure
(defmacro hygienic 
  [& body]
  (let [sym (gensym)]
    `(let [~sym :macro-value]
      ~@body))) 
;=> #'user/hygienic

(let [x :important-value]
  (hygienic (println "x:" x)))
; x: :important-value 
;=> nil
```

- auto-gensym을 활용한 문법 간소화: 문법 인용 구문 안에 #으로 끝나는 심벌에 자동으로 `gensym`이 적용됨

```clojure
(defmacro hygienic
  [& body]
  `(let [x# :macro-value]
      ~@body))
```

- 같은 문법 인용 내에 존재해야 같은 심벌로 인식함

```clojure
`(x# x#)
;=> (x__1447__auto__ x__1447__auto__)

[`x# `x#]
;=> [x__1450__auto__ x__1451__auto__] 
```
```clojure
(defmacro our-doto [expr & forms]
  `(let [obj# ~expr]
     ~@(map (fn [[f & args]]
              `(~f obj# ~@args)) forms)
     obj#))
; obj# 이 다른 문법인용 내에서 사용되어 매크로 사용시 에러가 발생함

(our-doto "It works"
  (println "I can't believe it"))
;=> CompilerException java.lang.RuntimeException: Unable to resolve symbol: obj__256537__auto__ in this context
```

- 이런 경우에는 manual하게 gensym을 사용해야 함
```clojure
(defmacro our-doto [expr & forms]
  (let [obj (gensym "obj")]
    `(let [~obj ~expr]
       ~@(map (fn [[f & args]]
                `(~f ~obj ~@args)) forms)  ;; `(~f ~obj ~@args)는 (list* f obj args)으로 작성하는 것이 낫다
       ~obj)))

(our-doto "It works"
  (println "I can't believe it")
  (println "I still can't believe it"))
; It works I can't believe it
; It works I still can't believe it 
;=> "It works"
```

### Leting the User Pick Names

- hygiene에 문제(variable shadowing)가 발생할 가능성이 있더라도 매크로에서 특정 이름의 바인딩을 사용하고, 이를 호출하는 측(caller)에서 사용해야 하는 경우가 있다. (예: `this`)
- 일반적으로 이러한 경우는 피해야 하지만, 꼭 사용해야 하는 경우에는 사용자가 바인딩에 사용할 심벌을 선택하도록 하는 것이 적절한 방법이다.

```clojure
(defmacro with
  [name & body]
  `(let [~name 5]
     ~@body))
;=> #'user/with

(with bar (+ 10 bar))
;=> 15

(with foo (+ 40 foo))
;=> 45
```

### Double Evaluation

```clojure
(defmacro spy [x] 
  `(do
    (println "spied" '~x ~x) 
    ~x))

(spy 2)
; spied 2 2
;=> 2

(spy (rand-int 10))
; spied (rand-int 10) 9     ;=> (1)
;=> 7                       ;=> (2)
; (1)과 (2)의 결과가 다르다

(macroexpand-1 '(spy (rand-int 10)))
;=> (do (println (rand-int 10))
;=>    (rand-int 10))
```

- 이러한 문제(두 번 이상 평가되어 각각 다른 결과를 낳는 현상)를 피하려면, 다음과 같이 작성한다.

```clojure
(defmacro spy [x] 
  `(let [x# ~x]
    (println "spied" '~x x#) 
    x#))

(macroexpand-1 '(spy (rand-int 10)))
;=> (let [x__725__auto__ (rand-int 10)]
;=> (println x__725__auto__ '(rand-int 10)) 
;=> x__725__auto__)

(spy (rand-int 10))
; spied (rand-int 10) 9 
;=> 9
```

- spy-helper 함수를 사용해서 double evaluation을 방지하는 방법
```clojure
(defn spy-helper [expr value] 
  (println expr value) value)

(defmacro spy [x] 
  `(spy-helper '~x ~x))
```

## Common Macro Idioms and Patterns

#### 로컬 바인딩을 정의하는 방법들
- `let, if-let, for, with-open` 등을 사용

```clojure
(let [a 42
      b "abc"]
  ...)

(if-let [x (test)] 
  then
  else)

(with-open [in (input-stream ...) 
            out (output-stream ...)]
  ...)

(for [x (range 10) 
      y (range x)]
  [x y])
;; range가 꼭 값이어야 할 필요 없음
```

#### var를 정의하는 매크로를 사용할 때 권장사항 (to be aligned with user expectations)
- def로 시작하는 이름 사용 (defn, defn-, defmacro, ...)
- var의 이름을 첫번째 인자로
- 매크로 당 하나의 var 정의
    - 두 개 이상의 var를 정의하면 사용자에게 혼란을 줄 수 있음
    - private! 과 같이 사용자가 직접 사용하지 않는 var는 예외

##### 너무 복잡하지 않게 작성
- 가능한 모든 동작은 함수에 위임. 평가의 제어가 필요한 경우에만 사용 (→ [When to Use Macros](https://github.com/ksseono/ClojureProgramming/blob/master/ch-05/ch-05-1.md#when-to-use-macros))


## The Implicit Arguments: &env and &form

- `defmacro` 는 두가지의 [로컬 바인딩](https://github.com/ksseono/ClojureProgramming/blob/master/ch-05/ch-05-2.md#leting-the-user-pick-names)을 제공한다: &env, &form

#### &env

- 특징: 현재의 모든 로컬 이름을 키로 하는 맵을 갖고 있음

```clojure
(defmacro spy-env []
  (let [ks (keys &env)]
    `(prn (zipmap '~ks [~@ks]))))

(let [x 1 y 2]
  (spy-env)
  (+ x y))
; {x 1, y 2} 
;=> 3
```

- 컴파일타임에 표현식을 평가하여 처리속도를 최적화는 용도로 사용할 수 있다

```clojure
;; 먼저 이러한 목적으로 사용할 매크로를 작성하고,

(defmacro simplify
  [expr]
  (let [locals (set (keys &env))]
    (if (some locals (flatten expr))    ;; 입력된 코드에 로컬이 있는지 확인
      expr                              ;; 로컬이 있으면 코드를 그대로 리턴
      (do
        (println "Precomputing: " expr)
        (list `quote (eval expr))))))   ;; 그렇지 않으면 컴파일타임에 표현식을 평가하여 리턴

;; 매크로를 사용했을 때와 사용하지 않았을 때를 비교

(defn f
  [a b c]
  (+ a b c (simplify (apply + (range 5e7)))))
; Precomputing:  (apply + (range 5.0E7))
;=> #'user/f

(f 1 2 3)
;=> 1249999975000006  ;; 결과가 바로 계산됨 (컴파일 타임에 range 계산을 해두었으므로)

(defn f'
  [a b c]
  (simplify (apply + a b c (range 5e7))))
;=> #'user/f'

(f' 1 2 3)
;=> 1249999975000006  ;; 약 1-2초 정도 소요 (런타임 계산)

;; f도 로컬을 사용하게 만들면 컴파일 타임 계산 불가능
(defn f
  [a b c]
  (+ a b c (simplify (apply + a (range 5e7)))))

(f 1 2 3)
;=> 1249999975000007  ;; 약 1-2초 정도 소요 (런타임 계산)
```

- &env 사용 테스트/디버깅하기
    - 명명된(named) var에 대한 역참조 문법 사용
    - 테스트할 매크로의 signature에 &form, &env 두 인자를 추가하여 호출하면 어떤 함수가 구현되는지 알 수 있음

```clojure
;; &form은 nil로, &env는 로컬(x)을 포함하는 경우와 포함하지 않는 경우 비교

(@#'simplify nil {} '(inc 1))
; Precomputing: (inc 1)
;=> (quote 2)

(@#'simplify nil {'x nil} '(inc x)) 
;=> (inc x)
```

#### &form

- &form: form as read by the reader 
- 사용자가 지정한 메티데이터도 모두 포함 (타입 힌트, 줄번호 등)

###### 활용 1) 매크로 에러 메시지 생성

```clojure
(defmacro ontology
  [& triples]
  (every? #(or (== 3 (count %))
               (throw (IllegalArgumentException.
                        "All triples provided as arguments must have 3 elements")))
          triples)
  ;; build and emit pre-processed ontology here... 
)
```

- 벡터가 트리플이 아닌 경우 (요소가 세 개 미만인 경우)에 예외 발생

```clojure
;; ontology 매크로 정의
(defmacro ontology
  [& triples]
  (every? #(or (== 3 (count %))
               (throw (IllegalArgumentException.
                        "All triples provided as arguments must have 3 elements")))
      triples)
  ;; build and emit pre-processed ontology here...
  )

;; 요소가 두 개인 벡터를 전달하면 에러 발생
(ontology ["Boston" :capital-of])
;=> #<IllegalArgumentException java.lang.IllegalArgumentException:
;=>   All triples provided as arguments must have 3 elements>

;; 스택트레이스를 출력해 봐도 유용한 정보(에러 위치)를 찾을 수 없음
(pst)
;=> IllegalArgumentException All triples provided as arguments must have 3 elements
;=>   macros/ontology/fn--825625 (form-init5648691076083813872.clj:3)
;=>   clojure.core/every? (core.clj:2550)
;=>   macros/ontology (form-init5648691076083813872.clj:3)
;=>   clojure.lang.Var.invoke (Var.java:388)
;=>   clojure.lang.Var.applyTo (Var.java:700)
;=>   clojure.lang.Compiler.macroexpand1 (Compiler.java:6631)
;=>   clojure.lang.Compiler.macroexpand (Compiler.java:6692)
;=>   clojure.lang.Compiler.eval (Compiler.java:6766)
;=>   clojure.lang.Compiler.eval (Compiler.java:6745)
;=>   clojure.core/eval (core.clj:3081)
;=>   clojure.main/repl/read-eval-print--7099/fn--7102 (main.clj:240)
;=>   clojure.main/repl/read-eval-print--7099 (main.clj:240)
```

이련 경우에 &form을 사용하면 도움이 된다.

```clojure
(defmacro ontology
  [& triples]
  (every? #(or (== 3 (count %))
             (throw (IllegalArgumentException.
                      (format "`%s` provided to `%s` on line %s has < 3 elements"
                        %       ;; 벡터 항목
                        (first &form)   ;; &form의 첫번째 항목은 매크로명
                        (-> &form meta :line)))))   ;; 매크로에서의 줄번호
    triples))

;; 결과
(ontology ["Boston" :capital-of])
;=> #<IllegalArgumentException java.lang.IllegalArgumentException:
;=> `["Boston" :capital-of]` provided to `ontology` on line 1 has < 3 elements>
```

###### 활용 2) 사용자가 제공한 타입힌트 보존

- 매크로는 일반적으로 form의 메타데이터(타입 힌트 포함)를 무시한다
- 다음 예제에서는 `or` 매크로에 부여한 타입힌트가 작동하지 않아 리플렉션 경고가 발생한다
```clojure
(defn first-char-of-either
  [a b]
  (.substring ^String (or a b) 0 1))
; Reflection warning, /private/var/folders/78/w8p67p3n43g_tnt3tckb4t2m0000gn/T/form-init5648691076083813872.clj:4:3 - call to method substring can't be resolved (target class is unknown).
;=> #'user/first-char-of-either
```

```clojure
;; 분명 타입힌트가 메타데이터에 포함됨에도 불구하고,
(binding [*print-meta* true] 
  (prn '^String (or a b)))
; ^{:tag String, :line 1} (or a b)

;; macroexpand를 해보면 타입힌트가 사라지는 것을 확인할 수 있음
(binding [*print-meta* true]
  (prn (macroexpand '^String (or a b))))
; (let* [or__3548__auto__ a]
;   (if or__3548__auto__ or__3548__auto__ (clojure.core/or b)))
```

- `or`는 이렇게 작성되어 있다 (`clojure.core`)
```clojure
(defmacro or 
  ([] nil)
  ([x] x) 
  ([x & next]
    `(let [or# ~x]
      (if or# or# (or ~@next)))))
```

- `or`매크로에 타입힌트가 포함되도록 다시 작성하면,

```clojure
(defmacro OR 
  ([] nil)
  ([x]
    (let [result (with-meta (gensym "res") (meta &form))]
      `(let [~result ~x] 
        ~result)))
  ([x & next]
    (let [result (with-meta (gensym "res") (meta &form))]
      `(let [or# ~x
            ~result (if or# or# (OR ~@next))]
        ~result))))
```
- 타입 힌트가 보존된다.
```clojure
(binding [*print-meta* true]
  (prn (macroexpand '^String (OR a b))))
; (let* [or__1176__auto__ a
;        ^{:tag String, :line 2}
;        res1186 (if or__1176__auto__ or__1176__auto__ (user/or b))]
;   ^{:tag String, :line 2} res1186)
```

- 이제 새로 정의한(타입 힌트가 보존되는) `OR`를 사용하면 경고가 발생하지 않는다
```clojure
(defn first-char-of-any 
  [a b]
  (.substring ^String (OR a b) 0 1)) 
;=> #'user/first-char-of-any
```

- `OR` 매크로에서 함수로 분리할 수 있는 부분을 떼어내어 재활용하면 다음과 같이 작성할 수 있다
```clojure
(defn preserve-metadata
  "Ensures that the body containing `expr` will carry the metadata from `&form`."
  [&form expr]
  (let [res (with-meta (gensym "res") (meta &form))]
    `(let [~res ~expr] 
      ~res)))

(defmacro OR
  "Same as `clojure.core/or`, but preserves user-supplied metadata (e.g. type hints)."
  ([] nil)
  ([x] (preserve-metadata &form x)) 
  ([x & next]
    (preserve-metadata &form `(let [or# ~x]
                                (if or# or# (or ~@next))))))
```

#### Testing Contextual Macros

- &form이나 &env를 사용하는 매크로는 테스트가 쉽지 않음 → 테스트/디버깅 때 &env를 효과적으로 살펴볼 수 있도록 다음과 같은 매크로를 작성

```clojure
(defn macroexpand1-env [env form]
  (if-let [[x & xs] (and (seq? form) (seq form))]
    (if-let [v (and (symbol? x) (resolve x))]
      (if (-> v meta :macro)
        (apply @v form env xs)
        form)
      form)
    form))
```
- `macroexpand1-env`를 사용해서 `simplify`가 서로 다른 환경(&env)에서 어떻게 동작하는지 테스트할 수 있음

```clojure
(macroexpand1-env '{} '(simplify (range 10)))
; Precomputing: (range 10)
;=> (quote (0 1 2 3 4 5 6 7 8 9))

(macroexpand1-env '{range nil} '(simplify (range 10))) 
;=> (range 10)
```

- `spy`매크로를 사용하는 코드에 대한 &form 메타데이터를 `macroexpand1-env`를 사용하여 테스트
```clojure
;; &form 정보를 추출해서 출력하는 spy 매크로 정의
(defmacro spy [expr] 
  `(let [value# ~expr]
    (println (str "line #" ~(-> &form meta :line) ",")
             '~expr value#) 
    value#))
;=> #'user/spy

;; spy 매크로 사용 테스트
(let [a 1
      a (spy (inc a))
      a (spy (inc a))] 
  a)
; line #2, (inc a) 2
; line #3, (inc a) 3 
;=> 3

;; macroexpand1-env를 사용하여 spy 매크로를 사용하는 코드 확장 (줄번호 메타데이터는 알아보기 쉽도록 임의로 입력)
(macroexpand1-env {} (with-meta '(spy (+ 1 1)) {:line 42})) 
;=> (clojure.core/let [value__602__auto__ (+ 1 1)]
;=>   (clojure.core/println
;=>     (clojure.core/str "line #" 42 ",")
;=>     (quote (+ 1 1)) value__602__auto__) 
;=>   value__602__auto__)
```
- `macroexpand1-env`의 효율적 작성
```clojure
;; if-let의 중첩 사용을 줄여주는 if-all-let 매크로가 있다고 가정하고 적용
(defn macroexpand1-env [env form]
  (if-all-let [[x & xs] (and (seq? form) (seq form))
                v (and (symbol? x) (resolve x))
                _ (-> v meta :macro)] 
    (apply @v form env xs)
    form))

;; 위와 같은 사용을 가능하게 해주는 if-all-let 작성
(defmacro if-all-let [bindings then else] 
  (reduce (fn [subform binding]
            `(if-let [~@binding] ~subform ~else)) 
    then (reverse (partition 2 bindings))))
```

## In Detail: -> and ->>

- `clojure.core`은 `->`, `->>`, `..` 등의 스레딩 매크로(threading macros)를 제공하고 있으며, 이들은 chained function 호출에 유용하게 사용된다.
- 이와 같은 기능을 하는 매크로 thread를 다음과 같이 구현해보자.

```clojure
;; (1) 구문이 시퀀스(seq)인지 확인하는 함수
(defn ensure-seq [x]
  (if (seq? x) x (list x)))

;; (2) x를 다음 시퀀스의 두번째 항목으로 추가하는 함수
;; 첫번째 버전
(defn insert-second
  "Insert x as the second item in seq y." 
  [x ys]
  (let [ys (ensure-seq ys)]
    (concat (list (first ys) x) 
            (rest ys))))

;; 두번째 버전: 매크로 정의할 때와 유사하게 syntax-quoting, unquoting 활용
(defn insert-second
  "Insert x as the second item in seq y." 
  [x ys]
  (let [ys (ensure-seq ys)]
    `(~(first ys) ~x ~@(rest ys))))

;; 세번째 버전: list* 활용
(defn insert-second
  "Insert x as the second item in seq y." 
  [x ys]
  (let [ys (ensure-seq ys)]
    (list* (first ys) x (rest ys))))
```

- insert-second를 활용하여 thread 매크로를 구현
```clojure
(defmacro thread
  "Thread x through successive forms."
  ([x] x)
  ([x form] (insert-second x form))
  ([x form & more] `(thread (thread ~x ~form) ~@more)))

;; 테스트 (-> 와 결과와 동일)
(thread [1 2 3] (conj 4) reverse println) 
;=> (4 3 2 1)

(-> [1 2 3] (conj 4) reverse println)
;=> (4 3 2 1)
```

- 그런데, 이 매크로는 함수로도 구현할 수 있다.

```clojure
(defn thread-fns
  ([x] x)
  ([x form] (form x))
  ([x form & more] (apply thread-fns (form x) more)))

(thread-fns [1 2 3] #(conj % 4) reverse  prn)
;=> (4 3 2 1)
```

- 그러나 이 함수는 `#()`과 같은 문법을 사용해야 해서 지저분하다

```clojure
;; thread 매크로 버전
(thread [1 2 3] .toString (.split " ") seq) 
;=> ("[1" "2" "3]")

;; thread-fns 함수 버전
(thread-fns [1 2 3] #(.toString %) #(.split % " ") seq)
;=> ("[1" "2" "3]")
```

## Final Thoughts
- 매크로는 리습에서와 마찬가지로 클로저에서도 강력한 표현력을 제공한다: 거추장스러운 표현의 제거, 코드 추상화 등
- 그러나 매크로 없이도 클로저의 함수형 프로그래밍과 데이터 모델링만으로도 추상화나 반복 패턴 작성 등의 많은 일을 처리할 수 있다. 
- 매크로는 코드를 단순화의 *마지막 단계*로 사용하는 것이 좋다.

