# Clojure의 Type.



* 기존 type에 새로운 method를, 그리고 기존 method에 새로운 type을


* 동적으로 type이 지정된다고 해서 Clojure에 type이 없는 것은 아니다.
* 단지 모든 type을 미리 선언하지 않아도 될 뿐이다.



## Expression Problem

* https://en.wikipedia.org/wiki/Expression_problem
* http://homepages.inf.ed.ac.uk/wadler/papers/expression/expression.txt
* [expression_problem.md](expression_problem.md)

* 기존 코드를 재컴파일하지 않으면서, 정적 유형을 안전하게 유지하면서 새 케이스를 데이터 유형에 추가하고 데이터 유형 전체에 새 함수를 추가 할 수 있는가.
* OOP언어 대부분은 상속을 통해, 혹은 몽키패칭을 통해, Expression Problem을 해결하려고 한다.
* OOP 언어를 이용하면, 기존 인터페이스를 상속받아 새로운 type을 쉽게 추가할 수 있다.
* functional 언어를 사용하면, 기존 type에 적용시킬 수 있는 새로운 함수(인터페이스)를 쉽게 추가할 수 있다.

* [기존 클래스에 메서드를 확장하기](http://blog.dahlia.kr/post/26202408156)
* [defrecord with "inheritance"](https://groups.google.com/forum/#!topic/clojure/mr-o9sRyiZ0)



## Clojure에서는?


```
 Clojure is a functional language
    that explicitly supports programs as models
    and provides robust and easy-to-use facilities for managing identity and state in a single process in the face of concurrency.

 Exactly one state at any point in time. And that state is a true value, i.e. it never changes.
  If an identity appears to change, it is because it becomes associated with different state values over time. This is the Clojure model.

 - http://clojure.org/state
```



### Local State
mutable?

* http://scattered-thoughts.net/blog/2014/02/17/local-state-is-harmful/
* http://blog.circleci.com/local-state-global-concerns/



### abstraction
```
 Clojure eschews the traditional object-oriented approach of creating a new data type for each new situation,
  instead preferring to build a large library of functions on a small set of types

  - http://clojure.org/multimethods
 ```


```
The protocols and datatypes features add powerful and flexible mechanisms for abstraction and data structure definition with no compromises vs the facilities of the host platform.

 - http://clojure.org/datatypes
```


```
프로토콜은 강력한 도구로 원래의 코드를 수정하지 않고도 이름이 충돌하지 않게 기존 클래스에 메소드를 새로 삽입할 수 있는 기능을 효과적으로 제공한다.
그러나 프로토콜은 Expression Problem의 절반, 즉 Wadler 테이블의 "새 열"에 대한 문제점만 해결할 뿐이다.
Clojure에서는 "새 행"을 테이블에 어떻게 추가할까?
해답은 바로 데이터 유형에 있다. 오브젝트 지향 환경에서는 데이터 유형이 클래스와 동일한 역할을 한다.

 - expression problem
```

* https://github.com/clojure/clojure/blob/master/src/clj/clojure/core/protocols.clj
* https://github.com/omcljs/om/blob/master/src/main/om/core.cljs
* https://github.com/clojure/core.async/blob/master/src/main/clojure/clojure/core/async/impl/channels.clj
* https://github.com/stuartsierra/component




## Practice
* https://github.com/cemerick/clojure-type-selection-flowchart/
* https://github.com/Prismatic/eng-practices/blob/master/clojure/20130926-data-representation.md




## Keywords
* type

* defprotocol
* deftype
* defrecord
* definterface
* rerify

* extend
* extend-type
* extend-protocol
* extenders

* extends?
* satisfies?
* intance?

* ^:volatile-mutable
* ^:unsynchronized-mutable
* :impls

* gen-class
* gen-interface

* ->CustomType
* map->CustobvmType
* CustomType/create


## defprotocol
* Protocols were introduced in Clojure 1.2.
* protocol is type-based single dispatch

* defprotocol
 - 주의. destructuring or rest arguments 를 사용하지 못함.
 * java interaface, clojure protocol
  - clojure의 protocol은 정의되는 시점에서 프로토콜 메소드는 일반 함수로 존재한다는 점에서, java interface와 다르다.


## deftype


```clojure
;; clojure의 type이 자바 class로 컴파일 되므로, 이름을 CamelCase로 짓는다.

(defprotocol IProtocol1
  (hello1 [this]))


;; clojure의 type이 자바 class로 컴파일 되므로, 이름을 CamelCase로 짓는다.
(deftype AType [val])

(AType. 10)
;=> #<AType hello.core.AType@f97d5d5>

(.val (AType. 10))
;=> 10


(extend AType
  IProtocol1
  {:hello1
   (fn [this]
     (+ (.val this) 10))})


(extend-protocol IProtocol1
  AType
  (hello1 [this]
    (+ (.val this) 10)))

(extend-type AType
  IProtocol1
  (hello1 [this]
    (+ (.val this) 10)))

(hello1 (AType. 10))
;=> 20


IProtocol1
;; {:impls
;;  {hello.core.AType {:hello1 #<core$eval16009$fn__16010 hello.core$eval16009$fn__16010@4cd10132>}},
;;  :on hello.core.IProtocol1,
;;  :on-interface hello.core.IProtocol1,
;;  :sigs {:hello1 {:doc nil, :arglists ([this]), :name hello1}},
;;  :var #'hello.core/IProtocol1,
;;  :method-map {:hello1 :hello1},
;;  :method-builders {#'hello.core/hello1 #<core$eval15949$fn__15950 hello.core$eval15949$fn__15950@67a295e>}}


(extenders IProtocol1)
;=> (hello.core.AType)

(extends? IProtocol1 AType)
;=> true


(satisfies? IProtocol1 (AType. 10))
;=> true


(instance? hello.core.AType (AType. 10))
;=> true

```


```clojure
;; var가 아닌, host 클래스이므로, 정의된 type을 명시적으로 import해야한다.

(def x "hello")
;= #'user/hello
(defrecord Point [x y])
;= user.Point
(Point. 5 5)
;= #user.Point{:x 5, :y 5}
(ns user2)
(refer 'user)
x
;= "hello"
Point
;= CompilerException java.lang.Exception:
;= Unable to resolve symbol: Point
(import 'user.Point)
Point
;= user.Point
```




```clojure
(defrecord NamedPoint [^String name ^long x ^long y])
(NamedPoint/getBasis)
;= [name x y]
(map meta (NamedPoint/getBasis))
;= ({:tag String} {:tag long} {:tag long})
```




```clojure
;; `volatile` here has the same meaning as the volatile field modifier in Java: reads and writes are atomic12 and must be executed in program order; i.e., they cannot be reordered by the JIT compiler or by the CPU. Volatiles are thus unsurprising and threadsafe — but uncoordinated and still entirely open to race conditions.
;; an `unsynchronized` field is a "regular" Java mutable field whose use will be thread-safe only under the guard of a lock or in the most expert of hands.


(defprotocol ITestMutable
  (set-val! [this])
  (get-val [this]))

(deftype MutableType [^:unsynchronized-mutable val]
  ITestMutable
  (set-val! [this]
    (set! val (+ val 10))
    this)
  (get-val [this]
    val))

(.val (MutableType. 10))
;-> IllegalArgumentException No matching field found: val for class hello.core.MutableType

(-> (MutableType. 10)
    (get-val))
;=> 10


(-> (MutableType. 10)
    (set-val!)
    (get-val))
;=> 20
```



## Records p272

* 사실, defrecord는 deftype을 이용해서 만들어진 macro.


* Value semantics
* Full participation in the associative collection abstraction
* Metadata support
* Reader support, so instances of record types can be created by simply reading data
* An additional convenience constructor for creating records with metadata and auxiliary fields as desired


```clojure
;; Records are associative collections.
(:x (Point. 3 4))
;= 3
(:z (Point. 3 4) 0)
;= 0
(map :x [(Point. 3 4)
(Point. 5 6)
(Point. 7 8)])
;= (3 5 7)


(:z (assoc (Point. 3 4) :z 5))
;= 5
(.z (assoc (Point. 3 4) :z 5))
;= #<java.lang.IllegalArgumentException:
;= No matching field found: z for class user.Point>



;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Readable representation.
(pr-str (assoc (Point. 3 4) :z [:a :b]))
;= "#user.Point{:x 3, :y 4, :z [:a :b]}"
(= (read-string *1)
(assoc (Point. 3 4) :z [:a :b]))

;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Auxiliary constructor.
(Point. 3 4 {:foo :bar} {:z 5})
;= #user.Point{:x 3, :y 4, :z 5}
(meta *1)
;= {:foo :bar}


;;;;;;;;;;;;;;;;;;;;;
;; Metadata support.
(-> (Point. 3 4)
  (with-meta {:foo :bar})
meta)
;= {:foo :bar}


;;;;;;;;;;;;;;;;;;;;;
;; Constructors and factory functions
(->Point 3 4)
;= #user.Point{:x 3, :y 4}

(map->Point {:x 3, :y 4, :z 5})
;= #user.Point{:x 3, :y 4, :z 5}

(map (partial apply ->Point) [[5 6] [7 8] [9 10]])
;= (#user.Point{:x 5, :y 6}
;= #user.Point{:x 7, :y 8}
;= #user.Point{:x 9, :y 10})
(map map->Point [{:x 1 :y 2} {:x 5 :y 6 :z 44}])
;= (#user.Point{:x 1, :y 2}
;= #user.Point{:x 5, :y 6, :z 44})


(Point/create {:x 3, :y 4, :z 5})
;= #user.Point{:x 3, :y 4, :z 5}


;;;;;;;;;;;;;;;;;;;;;
;;  defrecord automatically provides implementations, including java.util.Map, java.io.Serializable, clojure.lang.IPersistentMap
(defprotocol ClashWhenInlined
  (size [x]))

(defrecord R []
  ClashWhenInlined
  (size [x])) ; duplicate name error because the record implements a few standard interfaces, e.g. java.util.Map

(defrecord R [])

(extend-type R
  ClashWhenInlined
  (size [x])) ; no problem at all & and online updatable
```




## reify

```clojure
(defrecord Banana [qty])
(defrecord Grape  [qty])
(defrecord Orange [qty])

;;; 'subtotal' differs from each fruit.

(defprotocol Fruit
  (subtotal [item]))

(extend-type Banana
  Fruit
  (subtotal [item]
    (* 158 (:qty item))))

(extend-type Grape
  Fruit
  (subtotal [item]
    (* 178 (:qty item))))

(extend-type Orange
  Fruit
  (subtotal [item]
    (* 98 (:qty item))))

(defn coupon [item]
  (reify Fruit
    (subtotal [_]
      (int (* 0.75 (subtotal item))))))

(->> [(Orange. 10) (Banana. 15) (coupon (Grape. 10))]
     (map subtotal)
     (apply +))
;=> 4685



(defprotocol Foo
  (blah
    [this x]
    [this x y]))

;; define an anonymous extension via reify
(def r (reify Foo
         (blah [_ x] x)
         (blah [_ x y] y)))

;; invoke blah via the r instance
(blah r 1)
;; => 1
(blah r 1 2)
;; => 2
```

# gen-class && gen-interface
Wrapping Clojure in a Java API
http://david-mcneil.com/post/691347818/wrapping-clojure-in-a-java-api
